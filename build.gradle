buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
	}
}

apply plugin: 'java-library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'com.github.sikoried'
version = '1.0.3'
sourceCompatibility = 1.8

jar {
  manifest {
    attributes 'Implementation-Title': 'fjama', 'Implementation-Version': version
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "sikoried"
            name "Korbinian Riedhammer"
            email "korbinian@ieee.org"
        }
    }

    scm {
        url "https://github.com/sikoried/fjama"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.github.sikoried'
            artifactId 'fjama'
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Single precision port of NIST JAMA')
                root.appendNode('name', 'fjama')
                root.appendNode('url', 'https://github.com/sikoried/fjama')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = 'sikoried'
    key = System.getenv('BINTRAY_KEY')
    publish = true
    pkg {
        repo = 'github'
        name = 'fjama'
        userOrg = 'sikoried'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sikoried/fjama.git'
        version {
	        name = '1.0.3'
	        desc = 'Single precision JAMA'
	        released  = new Date()
	        vcsTag = 'master'
	    }
    }
}
